{#- Jinja Template for Sparql Query
 | Lists instances of certain type with their (optional) schema:name, skos:prefLabel, dc:title or rdfs:label
 | variables in template:
 |    - type: URI of the type to list
 |    - (optional) N: number of results you want (defaults to no limits --> full listing)
 |    - (optional) O: offset to apply when paging
 -#}
PREFIX dc:     <http://purl.org/dc/elements/1.1/>
PREFIX rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <https://schema.org/>
PREFIX skos:   <http://www.w3.org/2004/02/skos/core#>

SELECT 
  (?s as ?instance) 
  ?label
WHERE { 
    BIND (<{{ type }}>  as ?type)
	?s a ?type .
    OPTIONAL { ?s schema:name ?sorgname .}
    OPTIONAL { ?s dc:title ?dctitle .}
    OPTIONAL { ?s rdfs:label ?rdfslbl .}  
    OPTIONAL { ?s skos:prefLabel ?skoslbl .}
    BIND(coalesce( ?sorgname, ?dctitle, ?rdfslbl, ?skoslbl,  "«no name found»") as ?label)  # coalesce picks first one that is available
}
{{ "OFFSET " ~ O  if O else "" }} {#- add offset statement if provided #}
{{ "LIMIT " ~ N   if N else "" }} {#- add limit  statement if provided #}